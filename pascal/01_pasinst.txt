PASINST,T0,NT1.
RFL,120000.
REDUCE.
COMMENT.*
COMMENT.*  --------------------------  *
COMMENT.*  INSTALL THE PASCAL SYSTEM.  *
COMMENT.*  --------------------------  *
COMMENT.*
COMMENT.*
COMMENT.*  REQUEST THE TAPE.
COMMENT.*
VSN(TAPE=PAS000)
REQUEST(TAPE,PE,NORING)                REQUEST MAGNETIC TAPE.
COMMENT.*
COMMENT.*
COMMENT.*  REQUEST FILES.
COMMENT.*     COLDPL  = PASCAL COMPILER OLDPL.
COMMENT.*     LOLDPL  = PASCAL EXTENDED LIBRARY OLDPL.
COMMENT.*     PASCAL  = STOCK PASCAL COMPILER.
COMMENT.*     PASCLIB = STOCK PASCAL LIBRARY.
COMMENT.*     PSRCLIB = ERROR MESSAGES TEXTS.
COMMENT.*     UOLDPL  = PASCAL UTILITY PROGRAM OLDPL.
COMMENT.*
REQUEST(COLDPL,*PF,SN)
REQUEST(LOLDPL,*PF,SN)
REQUEST(PASCAL,*PF,SN)
REQUEST(PASCLIB,*PF,SN)
REQUEST(PSRCLIB,*PF,SN)
REQUEST(UOLDPL,*PF,SN)
COMMENT.*
COMMENT.*
COMMENT.*  CREATE OLDPLS.
COMMENT.*
REWIND(TEMP)
COPYBF(TAPE,TEMP)  JOLDPL
REWIND(TEMP)
COPYBF(TAPE,TEMP)
UPDATE(C=0,F,L=1,N=COLDPL,P=TEMP)
CATALOG(COLDPL,ID=PASCAL4,TK=A,PW=A)
REWIND(TEMP)
COPYBF(TAPE,TEMP)
UPDATE(C=0,F,L=1,N=LOLDPL,P=TEMP)
CATALOG(LOLDPL,ID=PASCAL4,TK=A,PW=A)
REWIND(TEMP)
COPYBF(TAPE,TEMP)
UPDATE(C=0,F,L=1,N=UOLDPL,P=TEMP)
CATALOG(UOLDPL,ID=PASCAL4,TK=A,PW=A)
COMMENT.*
COMMENT.*
COMMENT.*  COPY ERROR MESSAGE TEXTS.
COMMENT.*
REWIND(PSRCLIB)
COPYBF(TAPE,PSRCLIB)
COMMENT.*
COMMENT.*
COMMENT.*  CREATE PASTEXT.
COMMENT.*
UPDATE(L=1,P=COLDPL,Q)
REWIND(PASTEXT)
COMPASS(A,B=PASTEXT,I,L=0)
COMMENT.*
COMMENT.*
COMMENT.*  PREPARE COMPILER RELOCATABLE BINARY.
COMMENT.*
REWIND(PASCLGO)
COMPASS(A,B=PASCLGO,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT)
COPYBF(TAPE,PASCLGO)
COMMENT.*
COMMENT.*
COMMENT.*  PREPARE LIBRARY RELOCATABLE BINARY.
COMMENT.*
REWIND(PLIBLGO)
COMPASS(A,B=PLIBLGO,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT,S=IPTEXT)
COPYBF(TAPE,PLIBLGO)
COMMENT.*
COMMENT.*
COMMENT.*  CREATE PASCLIB[0].
COMMENT.*
REWIND(PLIBLGO)
REWIND(PASCLIB)
EDITLIB(USER)
COMMENT.*
COMMENT.*
COMMENT.*  CREATE PASCAL[0].
COMMENT.*
REWIND(PASCLGO)
REWIND(PASCAL)
LOAD(PASCLGO)
NOGO(PASCAL)
COMMENT.*
COMMENT.*
COMMENT.*
COMMENT.*  NOW REGENERATE THE COMPILER
COMMENT.*  AND MINIMAL LIBRARY.
COMMENT.*
COMMENT.*
COMMENT.*
UPDATE(F,I=0,L=1,N=0,P=COLDPL)
COMMENT.*
COMMENT.*
COMMENT.*  COPY HISTORY DECK.
COMMENT.*
REWIND(PASHIST)
COPYBR(COMPILE,PASHIST)
COMMENT.*
COMMENT.*
COMMENT.*  PREPARE ERROR TEXT PROCESSOR.
COMMENT.*
REWIND(LGO)
PASCAL(I,L=0,PL)
REWIND(LGO)
REWIND(INCLGEN)
LOAD(LGO)
NOGO(INCLGEN)
COMMENT.*
COMMENT.*
COMMENT.*  ASSEMBLE PASCAL-6000 SYSTEM TEXT[2].
COMMENT.*
REWIND(PASTEXT)
COMPASS(A,B=PASTEXT,I,L=0)
COMMENT.*
COMMENT.*
COMMENT.*  CREATE PASCAL[2].
COMMENT.*
REWIND(LGO)
COMPASS(A,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT)
PASCAL(I,L=0,PL)
REWIND(LGO)
REWIND(PASCAL)
LOAD(LGO)
NOGO(PASCAL)
COMMENT.*
COMMENT.*
COMMENT.*  CREATE MINIMAL PASCLIB[2].
COMMENT.*
REWIND(LGO)
REWIND(TEMP)
COPYBR(COMPILE,TEMP)
REWIND(TEMP)
REWIND(PSRCLIB)
INCLGEN(TEMP,PSRCLIB)
COMPASS(A,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT,S=IPTEXT)
PASCAL(I,L=0,PL)
BKSP(LGO,2)
COMPASS(A,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT,S=IPTEXT)
REWIND(LGO)
REWIND(PASCLIB)
EDITLIB(USER)
COMMENT.*
COMMENT.*
COMMENT.*
COMMENT.*  GENERATE FULL PASCAL LIBRARY.
COMMENT.*
COMMENT.*
COMMENT.*
UPDATE(F,I=0,L=1,N=0,P=LOLDPL)
COMMENT.*
COMMENT.*
COMMENT.*  COPY HISTORY DECK.
COMMENT.*
REWIND(LIBHIST)
COPYBR(COMPILE,LIBHIST)
COMMENT.*
COMMENT.*
COMMENT.*  PROCESS INCLUDE DECKS.
COMMENT.*
REWIND(LGO)
REWIND(TEMP)
COPYBR(COMPILE,TEMP)
REWIND(TEMP)
INCLGEN(TEMP,PSRCLIB)
COMMENT.*
COMMENT.*
COMMENT.*  COPY MINIMAL PASCLIB[2] RECORDS.
COMMENT.*
COMMENT.*
COMMENT.*
COMMENT.*  ADD LIBRARY ROUTINES.
COMMENT.*
PASCAL(I,L=0,PL)
BKSP(LGO,2)
PASCAL(I,L=0,PL)
BKSP(LGO)
REWIND(TEMP)
COPYBR(LGO,TEMP)
BKSP(LGO,2)
REWIND(TEMP)
COPYBR(TEMP,LGO)
COMPASS(A,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT)
REWIND(LGO)
REWIND(PASCLIB)
EDITLIB(USER)
COMMENT.*
COMMENT.*
COMMENT.*  UPDATE FILES.
COMMENT.*       INCLGEN = ERROR TEXT PROCESSOR.
COMMENT.*       PASHIST = COLDPL MODIFICATION HISTORY.
COMMENT.*       LIBHIST = LOLDPL MODIFICATION HISTORY.
COMMENT.*       PASTEXT = PASCAL SYSTEMS TEXT.
COMMENT.*
CATALOG(INCLGEN,ID=PASCAL4,TK=A,PW=A)
CATALOG(PASTEXT,ID=PASCAL4,TK=A,PW=A)
CATALOG(PASHIST,ID=PASCAL4,XR=A,PW=A)
CATALOG(LIBHIST,ID=PASCAL4,XR=A,PW=A)
CATALOG(PASCAL,ID=PASCAL4,XR=A,PW=A)
CATALOG(PASCLIB,ID=PASCAL4,XR=A,PW=A)
CATALOG(PSRCLIB,ID=PASCAL4,XR=A,PW=A)
COMMENT.
COMMENT. *** PASINST COMPLETE ***
EXIT(S)
COMMENT. *** PASINST FAILED ***
COMMENT.
~00
*/
*/  MODS FOR COMPILER INSTALLATION (IF NECESSARY) GO HERE.
*/
*IDENT    CONFIG1
*DECLARE  HISTORY
*I HISTORY.10
*
* CONFIG1 2022-09-21.        J. SIEBOLD
* 1. DEFINE OPERATING SYSTEM.
* 2. SET OPERATING SYSTEM NAME.
* 3. SET LOCAL SITE NAME.
* 4. CHANGE DEFAULT CHARACTER SET (IF APPROPRIATE).
* DECKS MODIFIED: COMSPAS, COMP.
*/
*/
*DECLARE  COMSPAS
*/
*/  SET TARGET OPERATING SYSTEM.
*/
*D COMSPAS.44
 NOSBE    =      1 ;         (* TARGET OPERATING SYSTEM IS NOS/BE *)
*/
*/
*DECLARE  COMP
*/
*/  SET OSNAME AND SITENAME.
*/
*D COMP.113,COMP.114
*/  ***** INSERT OSNAME AND SITENAME LINES HERE. *****
OSNAME = 'NOS/BE 1.5';
SITENAME = 'CYBER 865                               ';
*/
*/  CHANGE DEFAULT CHARACTER SET (IF APPROPRIATE).
*/
*/        END OF MODSET "CONFIG1".
~00
*/
*/  MODS FOR LIBRARY INSTALLATION (IF NECESSARY) GO HERE.
*/
*IDENT    CONFIGL
*DECLARE  HISTORY
*I HISTORY.10
*
* CONFIGL 1985-06-06.        D. J. BIANCHI.
* YANK NOS INCLUDE PACKAGES.
*/
*DECLARE
*YANKDECK NOSINCL
*/        END OF MODSET "CONFIGL".
~00
*/
*/  MODS TO THE UTILITIES CAN BE INSERTED HERE.
*/
~00
*COMPILE PASTEXT,EOR2
*COMPILE COMPOVL,COMPRTN,EOR4
*COMPILE PSYSTM.REC
*COMPILE ZFN.WRS
~00
LIBRARY(PASCLIB,NEW)
ADD(*,PLIBLGO)
FINISH.
ENDRUN.
~00
LIBRARY(PASCLIB,OLD)
REPLACE(*,LGO)
FINISH.
ENDRUN.
~00
LIBRARY(PASCLIB,OLD)
REPLACE(*,LGO)
FINISH.
ENDRUN.
~00
}

