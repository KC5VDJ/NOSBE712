PAS6K,T0,NT1.
RFL,120000.
ATTACH,USERNUC,ID=CCT.
LIBRARY,USERNUC.
REDUCE.
COMMENT.*
COMMENT.*  --------------------------  *
COMMENT.*  INSTALL THE PASCAL SYSTEM.  *
COMMENT.*  --------------------------  *
COMMENT.*
COMMENT.*
COMMENT.*  REQUEST THE TAPE.
COMMENT.*
VSN(TAPE=PAS000)
REQUEST(TAPE,PE,NORING)                REQUEST MAGNETIC TAPE.
COMMENT.*
COMMENT.*
COMMENT.*     COLDPL  = PASCAL COMPILER OLDPL.
COMMENT.*     LOLDPL  = PASCAL EXTENDED LIBRARY OLDPL.
COMMENT.*     PASCAL  = STOCK PASCAL COMPILER.
COMMENT.*     PASCLIB = STOCK PASCAL LIBRARY.
COMMENT.*     PSRCLIB = ERROR MESSAGES TEXTS.
COMMENT.*     UOLDPL  = PASCAL UTILITY PROGRAM OLDPL.
COMMENT.*
REWIND(TEMP)
COPYBF(TAPE,TEMP)  JOLDPL
REWIND(TEMP)
COPYBF(TAPE,TEMP)
UPDATE(C=0,F,L=1,N=COLDPL,P=TEMP)
REWIND(TEMP)
COPYBF(TAPE,TEMP)
UPDATE(C=0,F,L=1,N=LOLDPL,P=TEMP)
REWIND(TEMP)
COPYBF(TAPE,TEMP)
UPDATE(C=0,F,L=1,N=UOLDPL,P=TEMP)
COMMENT.*
COMMENT.*
COMMENT.*  COPY ERROR MESSAGE TEXTS.
COMMENT.*
REWIND(PSRCLIB)
COPYBF(TAPE,PSRCLIB)
COMMENT.*
COMMENT.*
COMMENT.*  CREATE PASTEXT.
COMMENT.*
UPDATE(L=1,P=COLDPL,Q)
REWIND(PASTEXT)
COMPASS(A,B=PASTEXT,I,L=0)
COMMENT.*
COMMENT.*
COMMENT.*  PREPARE COMPILER RELOCATABLE BINARY.
COMMENT.*
REWIND(PASCLGO)
COMPASS(A,B=PASCLGO,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT)
COPYBF(TAPE,PASCLGO)
COMMENT.*
COMMENT.*
COMMENT.*  PREPARE LIBRARY RELOCATABLE BINARY.
COMMENT.*
REWIND(PLIBLGO)
COMPASS(A,B=PLIBLGO,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT,S=IPTEXT)
COPYBF(TAPE,PLIBLGO)
COMMENT.*
COMMENT.*
COMMENT.*  CREATE PASCLIB[0].
COMMENT.*
REWIND(PLIBLGO)
REWIND(PASCLIB)
EDITLIB(USER)
COMMENT.*
COMMENT.*
COMMENT.*  CREATE PASCAL[0].
COMMENT.*
REWIND(PASCLGO)
REWIND(PASCAL)
LOAD(PASCLGO)
NOGO(PASCAL)
COMMENT.*
COMMENT.*
COMMENT.*
COMMENT.*  NOW REGENERATE THE COMPILER
COMMENT.*  AND MINIMAL LIBRARY.
COMMENT.*
COMMENT.*
COMMENT.*
UPDATE(F,I=0,L=1,N=0,P=COLDPL)
COMMENT.*
COMMENT.*
COMMENT.*  COPY HISTORY DECK.
COMMENT.*
REWIND(PASHIST)
COPYBR(COMPILE,PASHIST)
COMMENT.*
COMMENT.*
COMMENT.*  PREPARE ERROR TEXT PROCESSOR.
COMMENT.*
REWIND(LGO)
PASCAL(I,L=0,PL)
REWIND(LGO)
REWIND(INCLGEN)
LOAD(LGO)
NOGO(INCLGEN)
COMMENT.*
COMMENT.*
COMMENT.*  ASSEMBLE PASCAL-6000 SYSTEM TEXT[2].
COMMENT.*
REWIND(PASTEXT)
COMPASS(A,B=PASTEXT,I,L=0)
COMMENT.*
COMMENT.*
COMMENT.*  CREATE PASCAL[2].
COMMENT.*
REWIND(LGO)
COMPASS(A,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT)
PASCAL(I,L=0,PL)
REWIND(LGO)
REWIND(PASCAL)
LOAD(LGO)
NOGO(PASCAL)
COMMENT.*
COMMENT.*
COMMENT.*  CREATE MINIMAL PASCLIB[2].
COMMENT.*
REWIND(LGO)
REWIND(TEMP)
COPYBR(COMPILE,TEMP)
REWIND(TEMP)
REWIND(PSRCLIB)
INCLGEN(TEMP,PSRCLIB)
COMPASS(A,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT,S=IPTEXT)
PASCAL(I,L=0,PL)
BKSP(LGO,2)
COMPASS(A,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT,S=IPTEXT)
REWIND(LGO)
REWIND(PASCLIB)
EDITLIB(USER)
COMMENT.*
COMMENT.*
COMMENT.*
COMMENT.*  GENERATE FULL PASCAL LIBRARY.
COMMENT.*
COMMENT.*
COMMENT.*
UPDATE(F,I=0,L=1,N=0,P=LOLDPL)
COMMENT.*
COMMENT.*
COMMENT.*  COPY HISTORY DECK.
COMMENT.*
REWIND(LIBHIST)
COPYBR(COMPILE,LIBHIST)
COMMENT.*
COMMENT.*
COMMENT.*  PROCESS INCLUDE DECKS.
COMMENT.*
REWIND(LGO)
REWIND(TEMP)
COPYBR(COMPILE,TEMP)
REWIND(TEMP)
INCLGEN(TEMP,PSRCLIB)
COMMENT.*
COMMENT.*
COMMENT.*  COPY MINIMAL PASCLIB[2] RECORDS.
COMMENT.*
COMMENT.*
COMMENT.*
COMMENT.*  ADD LIBRARY ROUTINES.
COMMENT.*
PASCAL(I,L=0,PL)
BKSP(LGO,2)
PASCAL(I,L=0,PL)
BKSP(LGO)
REWIND(TEMP)
COPYBR(LGO,TEMP)
BKSP(LGO,2)
REWIND(TEMP)
COPYBR(TEMP,LGO)
COMPASS(A,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT)
REWIND(LGO)
REWIND(PASCLIB)
EDITLIB(USER)
COMMENT.*
COMMENT.*
COMMENT.*  UPDATE FILES.
COMMENT.*       INCLGEN = ERROR TEXT PROCESSOR.
COMMENT.*       PASHIST = COLDPL MODIFICATION HISTORY.
COMMENT.*       LIBHIST = LOLDPL MODIFICATION HISTORY.
COMMENT.*       PASTEXT = PASCAL SYSTEMS TEXT.
COMMENT.*
COMMENT.* PASINST END
COMMENT.*
COMMENT.*  ----------------------------  *
COMMENT.*  CONSTRUCT THE PASCAL SYSTEM.  *
COMMENT.*  ----------------------------  *
COMMENT.*
COMMENT.*
COMMENT.*  INPUT FILES REQUIRED FOR PASCOMP.
COMMENT.*       NAME      DESCRIPTION
COMMENT.*       OLDPASC   OLD PASCAL COMPILER BINARY.
COMMENT.*       OLDPLIB   OLD PASCAL LIBRARY BINARY.
COMMENT.*       PSRCLIB   ERROR TEXTS.
COMMENT.*       COLDPL    OLD PASCAL COMPILER OLDPL.
COMMENT.*       LOLDPL    OLD PASCAL EXTENDED LIBRARY OLDPL.
COMMENT.*  OBTAIN PASCAL[0] AND PASCLIB[0] AS OLDPASC AND OLDPLIB.
COMMENT.*
COMMENT.*
COMMENT.*  OUTPUT FILES CREATED BY PASCOMP.
COMMENT.*       NAME      DESCRIPTION
COMMENT.*       PASCAL    NEW PASCAL COMPILER BINARY.
COMMENT.*       PASCLIB   NEW PASCAL LIBRARY BINARY.
COMMENT.*       PSRCLIB   NEW PASCAL SOURCE LIBRARY.
COMMENT.*       CNEWPL    NEW PASCAL COMPILER OLDPL.
COMMENT.*       LNEWPL    NEW PASCAL EXTENDED LIBRARY OLDPL.
COMMENT.*       INCLGEN   INCLGEN PROGRAM ABSOLUTE BINARY.
COMMENT.*       PASHIST   COLDPL HISTORY DECK.
COMMENT.*       LIBHIST   LOLDPL HISTORY DECK.
COMMENT.*       PASTEXT   PASCAL SYSTEMS TEXT.
COMMENT.*
COMMENT.*
COMMENT.*
COMMENT.*  PREPARE NEW COMPILER AND LIBRARY.
COMMENT.*
COMMENT.*
COMMENT.*
COMMENT.*  APPLY FIRST MODS.
COMMENT.*
UPDATE(F,L=1,N=NEWPL,P=COLDPL)
COMMENT.*
COMMENT.*
COMMENT.*  SKIP HISTORY DECK.
COMMENT.*
SKIPF(COMPILE,1)
COMMENT.*
COMMENT.*
COMMENT.*  PREPARE ERROR TEXT PROCESSOR.
COMMENT.*
REWIND(LGO)
PASCAL(I,L=0,PL)
REWIND(LGO)
REWIND(INCLGEN)
LOAD(LGO)
NOGO(INCLGEN)
COMMENT.*
COMMENT.*
COMMENT.*  ASSEMBLE PASCAL-6000 SYSTEM TEXT[1].
COMMENT.*
REWIND(PASTEXT)
COMPASS(A,B=PASTEXT,I,L=0)
COMMENT.*
COMMENT.*
COMMENT.*  CREATE PASCAL[1].
COMMENT.*
REWIND(LGO)
COMPASS(A,I,L=A,G=PASTEXT,S=CPUTEXT,S=PFMTEXT)
PASCAL(I,L=0,PL)
REWIND(LGO)
REWIND(PASCAL)
COMMENT.*
COMMENT.*  REQUEST NEW COMPILER.
COMMENT.*
RETURN(PASCAL)
LOAD(LGO)
NOGO(PASCAL)
COMMENT.*
COMMENT.*
COMMENT.*  CREATE MINIMAL PASCLIB[1].
COMMENT.*
REWIND(LGO)
REWIND(TEMP)
COPYBR(COMPILE,TEMP)
REWIND(TEMP)
COMMENT.*
COMMENT.*  REQUEST NEW SOURCE LIBRARY.
COMMENT.*
RETURN(PSRCLIB)
REQUEST(PSRCLIB,SN,*PF)
INCLGEN(TEMP,PSRCLIB)
COMPASS(A,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT,S=IPTEXT)
PASCAL(I,L=0,PL)
BKSP(LGO,2)
COMPASS(A,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT,S=IPTEXT)
REWIND(LGO)
REWIND(PASCLIB)
COMMENT.*
COMMENT.*  REQUEST NEW LIBRARY.
COMMENT.*
RETURN(PASCLIB)
EDITLIB(USER)
COMMENT.*
COMMENT.*
COMMENT.*
COMMENT.*  NOW DO IT AGAIN TO COMPLETE
COMMENT.*  A FULL BOOTSTRAP.
COMMENT.*
COMMENT.*
COMMENT.*
COMMENT.*  APPLY SECOND MODS.
COMMENT.*
UPDATE(F,L=1,N=CNEWPL,P=NEWPL)
COMMENT.*
COMMENT.*
COMMENT.*  COPY HISTORY DECK.
COMMENT.*
REWIND(PASHIST)
COPYBR(COMPILE,PASHIST)
COMMENT.*
COMMENT.*
COMMENT.*  SKIP ERROR TEXT PROCESSOR.
COMMENT.*
SKIPF(COMPILE,1)
COMMENT.*
COMMENT.*
COMMENT.*  ASSEMBLE PASCAL-6000 SYSTEM TEXT[2].
COMMENT.*
REWIND(PASTEXT)
COMPASS(A,B=PASTEXT,I,L=0)
COMMENT.*
COMMENT.*
COMMENT.*  CREATE PASCAL[2].
COMMENT.*
REWIND(LGO)
COMPASS(A,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT)
PASCAL(I,L=0,PL)
REWIND(LGO)
REWIND(PASCAL)
LOAD(LGO)
NOGO(PASCAL)
COMMENT.*
COMMENT.*
COMMENT.*  CREATE MINIMAL PASCLIB[2].
COMMENT.*
REWIND(LGO)
REWIND(TEMP)
COPYBR(COMPILE,TEMP)
REWIND(TEMP)
REWIND(PSRCLIB)
INCLGEN(TEMP,PSRCLIB)
COMPASS(A,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT,S=IPTEXT)
PASCAL(I,L=0,PL)
BKSP(LGO,2)
COMPASS(A,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT,S=IPTEXT)
REWIND(LGO)
REWIND(PASCLIB)
EDITLIB(USER)
COMMENT.*
COMMENT.*
COMMENT.*
COMMENT.*  GENERATE FULL PASCAL LIBRARY.
COMMENT.*
COMMENT.*
COMMENT.*
COMMENT.*  APPLY LIBRARY MODS.
COMMENT.*
UPDATE(F,L=1,N=LNEWPL,P=LOLDPL)
COMMENT.*
COMMENT.*
COMMENT.*  COPY HISTORY DECK.
COMMENT.*
REWIND(LIBHIST)
COPYBR(COMPILE,LIBHIST)
COMMENT.*
COMMENT.*
COMMENT.*  PROCESS INCLUDE DECKS.
COMMENT.*
REWIND(LGO)
REWIND(TEMP)
COPYBR(COMPILE,TEMP)
REWIND(TEMP)
INCLGEN(TEMP,PSRCLIB)
COMMENT.*
COMMENT.*
COMMENT.*  COPY MINIMAL PASCLIB[2] RECORDS.
COMMENT.*
COMMENT.*
COMMENT.*
COMMENT.*  ADD LIBRARY ROUTINES.
COMMENT.*
PASCAL(I,L=0,PL)
BKSP(LGO,2)
PASCAL(I,L=0,PL)
BKSP(LGO)
REWIND(TEMP)
COPYBR(LGO,TEMP)
BKSP(LGO,2)
REWIND(TEMP)
COPYBR(TEMP,LGO)
COMPASS(A,I,L=0,G=PASTEXT,S=CPUTEXT,S=PFMTEXT)
REWIND(LGO)
REWIND(PASCLIB)
EDITLIB(USER)
COMMENT.*
COMMENT.*
COMMENT.*  UPDATE FILES.
COMMENT.*
CATALOG(PSRCLIB,ID=PASCAL4,XR=A,PW=A)
COMMENT.* PASCOMP END
COMMENT.*
COMMENT.*  ----------------------------------  *
COMMENT.*  COMPILE THE PASCAL-6000 UTILITIES.  *
COMMENT.*  ----------------------------------  *
COMMENT.*
COMMENT.*
COMMENT.*  INPUT FILES REQUIRED FOR UTICOMP.
COMMENT.*       NAME      DESCRIPTION
COMMENT.*
COMMENT.*       UOLDPL    OLD UTILITIES PROGRAM LIBRARY.
COMMENT.*       PASCAL    PASCAL COMPILER BINARY.
COMMENT.*       PASCLIB   PASCAL LIBRARY.
COMMENT.*       PSRCLIB   PASCAL TEXT LIBRARY.
COMMENT.*
COMMENT.*
COMMENT.*
COMMENT.*  APPLY MODIFICATIONS.
COMMENT.*
UPDATE(F,L=1,P=UOLDPL)
COMMENT.*
REQUEST(ANALYZE,*PF)
COMMENT.*
COMMENT.*  COMPILE ANALYZE.
COMMENT.*
REWIND(LGO)
PASCAL(I,L=0,PL)
LOAD(LGO)
NOGO(ANALYZE)
CATALOG(ANALYZE,ID=PASCAL4)
COMMENT.*
COMMENT.*
COMMENT.*  COMPILE AUGMENT.
COMMENT.*
REQUEST(AUGMENT,*PF)
REWIND(LGO)
PASCAL(I,L=0,PL)
LOAD(LGO)
NOGO(AUGMENT)
CATALOG(AUGMENT,ID=PASCAL4)
COMMENT.*
COMMENT.*
COMMENT.*  COMPILE COMPARE.
COMMENT.*
REQUEST(COMPARE,*PF)
REWIND(LGO)
PASCAL(I,L=0,PL)
LOAD(LGO)
NOGO(COMPARE)
CATALOG(COMPARE,ID=PASCAL4)
COMMENT.*
COMMENT.*
COMMENT.*  COMPILE PASCODE.
COMMENT.*
REQUEST(PASCODE,*PF)
REWIND(LGO)
PASCAL(I,L=0,PL)
LOAD(LGO)
NOGO(PASCODE)
CATALOG(PASCODE,ID=PASCAL4)
COMMENT.*
COMMENT.*
COMMENT.*  COMPILE PASCREF.
COMMENT.*
REQUEST(PASCREF,*PF)
REWIND(LGO)
PASCAL(I,L=0,PL)
LOAD(LGO)
NOGO(PASCREF)
CATALOG(PASCREF,ID=PASCAL4)
COMMENT.*
COMMENT.*
COMMENT.*  COMPILE PASPPIX.
COMMENT.*
REQUEST(PASPPIX,*PF)
REWIND(LGO)
PASCAL(I,L=0,PL)
LOAD(LGO)
NOGO(PASPPIX)
CATALOG(PASPPIX,ID=PASCAL4)
COMMENT.*
COMMENT.*
COMMENT.*  COMPILE PASVERT.
COMMENT.*
REQUEST(PASVERT,*PF)
REWIND(LGO)
PASCAL(I,L=0,PL)
LOAD(LGO)
NOGO(PASVERT)
CATALOG(PASVERT,ID=PASCAL4)
COMMENT.*
COMMENT.*
COMMENT.*  COMPILE PRETTY.
COMMENT.*
REQUEST(PRETTY,*PF)
REWIND(LGO)
PASCAL(I,L=0,PL)
LOAD(LGO)
NOGO(PRETTY)
CATALOG(PRETTY,ID=PASCAL4)
COMMENT.*
COMMENT.*
COMMENT.*  COMPILE PROSE.
COMMENT.*
REQUEST(PROSE,*PF)
REWIND(LGO)
PASCAL(I,L=0,PL)
LOAD(LGO)
NOGO(PROSE)
CATALOG(PROSE,ID=PASCAL4)
COMMENT.*
COMMENT.*
COMMENT.*  COMPILE SPRUCE.
COMMENT.*
REQUEST(SPRUCE,*PF)
REWIND(LGO)
PASCAL(I,L=0,PL)
LOAD(LGO)
NOGO(SPRUCE)
CATALOG(SPRUCE,ID=PASCAL4)
COMMENT.*
COMMENT.*
COMMENT.*  COMPILE WRITE.
COMMENT.*
REQUEST(WRITE,*PF)
REWIND(LGO)
PASCAL(I,L=0,PL)
COMPASS(I,L=0)
LOAD(LGO)
NOGO(WRITE)
CATALOG(WRITE,ID=PASCAL4)
LIBRARY.
RETURN(USERNUC)
ATTACH,USERNUC,ID=CCT,PW=XYZ,RW=1.
LIBRARY(USERNUC)
EDITLIB(SYSTEM,MSGL=6,ERROR=3)
EXTEND(USERNUC)
ATTACH,UPASLIB,ID=CCT,PW=XYZ,RW=1.
EDITLIB(SYSTEM,MSGL=6,ERROR=3)
EXTEND(UPASLIB)
RETURN,UPASLIB.
COMMENT. *** PAS6K COMPLETE ***
EXIT(S)
COMMENT. *** PAS6K FAILED ***
~eor
*/
*/  MODS FOR COMPILER INSTALLATION (IF NECESSARY) GO HERE.
*/
*IDENT    CONFIG1
*DECLARE  HISTORY
*I HISTORY.10
*
* CONFIG1 2022-09-21.        J. SIEBOLD
* 1. DEFINE OPERATING SYSTEM.
* 2. SET OPERATING SYSTEM NAME.
* 3. SET LOCAL SITE NAME.
* 4. CHANGE DEFAULT CHARACTER SET (IF APPROPRIATE).
* DECKS MODIFIED: COMSPAS, COMP.
*/
*/
*DECLARE  COMSPAS
*/
*/  SET TARGET OPERATING SYSTEM.
*/
*D COMSPAS.44
 NOSBE    =      1 ;         (* TARGET OPERATING SYSTEM IS NOS/BE *)
*/
*/
*DECLARE  COMP
*/
*/  SET OSNAME AND SITENAME.
*/
*D COMP.113,COMP.114
*/  ***** INSERT OSNAME AND SITENAME LINES HERE. *****
OSNAME = 'NOS/BE 1.5';
SITENAME = 'CYBER 865                               ';
*/
*/  CHANGE DEFAULT CHARACTER SET (IF APPROPRIATE).
*/
*/        END OF MODSET "CONFIG1".
~eor
*/
*/  MODS FOR LIBRARY INSTALLATION (IF NECESSARY) GO HERE.
*/
*IDENT    CONFIGL
*DECLARE  HISTORY
*I HISTORY.10
*
* CONFIGL 1985-06-06.        D. J. BIANCHI.
* YANK NOS INCLUDE PACKAGES.
*/
*DECLARE
*YANKDECK NOSINCL
*/        END OF MODSET "CONFIGL".
~eor
*/
*/  MODS TO THE UTILITIES CAN BE INSERTED HERE.
*/
~eor
*COMPILE PASTEXT,EOR2
*COMPILE COMPOVL,COMPRTN,EOR4
*COMPILE PSYSTM.REC
*COMPILE ZFN.WRS
~eor
LIBRARY(PASCLIB,NEW)
ADD(*,PLIBLGO)
FINISH.
ENDRUN.
~eor
LIBRARY(PASCLIB,OLD)
REPLACE(*,LGO)
FINISH.
ENDRUN.
~eor
LIBRARY(PASCLIB,OLD)
REPLACE(*,LGO)
FINISH.
ENDRUN.
~eor
*/
*/  MODS FOR FIRST COMPILATION GO HERE.
*/
~eor
LIBRARY(PASCLIB,NEW)
ADD(*,LGO)
FINISH.
ENDRUN.
~eor
*/
*/  MODS FOR SECOND COMPILATION GO HERE.
*/
~eor
LIBRARY(PASCLIB,OLD)
REPLACE(*,LGO)
FINISH.
ENDRUN.
~eor
*/
*/  MODS FOR LIBRARY COMPILATION GO HERE.
*/
~eor
LIBRARY(PASCLIB,OLD)
REPLACE(*,LGO)
FINISH.
ENDRUN.
~eor
*/
*/  MODS TO THE UTILITIES CAN BE INSERTED HERE.
*/
~eor
LIBRARY(USERNUC,OLD)
REPLACE(PASCAL,PASCAL,AL=3,FL=56000,FLO=0)
FINISH.
ENDRUN.
~eor
LIBRARY(UPASLIB,OLD)
ADD(*,PASCLIB)
ADD(*,PASTEXT)
FINISH.
ENDRUN.
~eoi
